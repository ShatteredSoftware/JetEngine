import de.undercouch.gradle.tasks.download.Download
import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        jcenter()
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath group: 'com.github.rodionmoiseev.gradle.plugins', name: 'idea-utils', version: '0.2'
    }
}

plugins {
    id "com.gradle.build-scan" version "1.16"
    id "com.github.johnrengelman.shadow" version "1.2.3"
    id "de.undercouch.download" version "3.1.1"
    id "org.sonarqube" version "2.6"
}


buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'idea-utils'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: "jacoco"
//apply plugin: 'findbugs'

idea {
    project {
        ipr {
            withXml { xmlProvider ->
                def project = xmlProvider.asNode()
                def runConfigs = new XmlParser().parse(new File("config/intellij_runconfigs.xml"))
                project.append(runConfigs)
            }
        }
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

group = project.property("group")
version = project.property("version")
sourceCompatibility = project.property("sourceversion")

dependencies {
    compile group: 'org.spigotmc', name: 'spigot-api', version: project.property("apiversion")
    compile group: 'org.spigotmc', name: 'spigot', version: project.property("apiversion")

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.8.0'
    testCompile 'org.powermock:powermock-api-mockito2:1.7.0RC2'
    testCompile 'org.powermock:powermock-module-junit4:1.7.0'
    testCompile 'org.powermock:powermock-core:1.7.0'
    testCompile 'org.powermock:powermock-module-junit4-rule:1.7.0'

    // dependencies go here
    // Example:
    // compile group: 'com.google.guava', name: 'guava', version: '19.0'
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.property("group")
            artifactId = project.property("name")
            version = project.property("version")

            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                description = 'A spigot plugin to make spigot plugins easier.'
            }
        }
    }
}

processResources {
    filter ReplaceTokens, tokens: [
            "apiversion": project.property("apiversion"),
            "version"   : project.property("version")
    ]
}

clean {
    delete "final/"
    delete "testserver/"
}

jacocoTestReport {
    group = "Reporting"
    reports {
        xml.enabled true
        csv.enabled false
        html.destination "${buildDir}/reports/coverage"
    }
}

compileJava.dependsOn {
//    check
    buildSpigot
}

jar {
    actions = []
    dependsOn = []
    dependsOn('shadowJar')
}

shadowJar {
    shadowJar.archiveName = "JetEngine-${version}.jar"
    dependencies {
        exclude(dependency('org.spigotmc:spigot-api:.*'))
        exclude(dependency('org.spigotmc:spigot:.*'))
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

task deploy(type: Copy, dependsOn: ['build']) {
    from "${buildDir}/libs"
    into "final/"
}

task setupDevServer(dependsOn: 'extractServerJar', type: Copy) {
    from 'config/serverfiles'
    into 'testserver'
}

task extractServerJar(type: Copy) {
    from {
        configurations.runtime
    }
    include("spigot-" + project.property("apiversion") + ".jar")
    rename("spigot-" + project.property("apiversion") + ".jar", "server.jar")
    into "testserver/"
}

task copyPluginToTestserver(dependsOn: ['build'], type: Copy) {
    from "${buildDir}/libs"
    into "testserver/plugins"
}

task prepareDevServer(dependsOn: ['buildSpigot', 'setupDevServer', 'copyPluginToTestserver']) {}

task startDevServer(dependsOn: [prepareDevServer], type: JavaExec) {
    classpath configurations.compile, configurations.runtime
    main = "org.bukkit.craftbukkit.Main"
    workingDir = "testserver/"
    standardInput = System.in
}

// START Building Spigot and Bukkit

def spigotBuildDir = new File("$buildDir/spigot/")

task setupWorkspace(dependsOn: ['buildSpigot']) {
}

task buildSpigot(type: JavaExec) {
    if (hasSpigot()) {
        enabled = false;
        dependsOn = [];
    } else {
        dependsOn = ['cleanSpigotBuild', 'downloadBuildTool']
    }
    main = '-jar'
    args new File(spigotBuildDir, "BuildTools.jar").getAbsolutePath(), "--rev", project.property("apibuildtoolversion")
    workingDir = spigotBuildDir
}

task downloadBuildTool(type: Download) {
    spigotBuildDir.mkdirs()
    src project.property("buildtoolurl")
    dest new File(spigotBuildDir, "BuildTools.jar")
}

task cleanSpigotBuild() {
    spigotBuildDir.deleteDir()
}

def hasSpigot() {
    def groupId = "org.spigotmc"
    def version = project.property("apiversion")
    return hasArtifact(groupId, "spigot-api", version) && hasArtifact(groupId, "spigot", version)
}

def hasArtifact(groupId, artifactId, version) {
    def localMavenRepo = new File(new File(ant.properties['user.home'] as String), ".m2/repository/")
    def file = new File(localMavenRepo, groupId.replace('.', '/') + "/" + artifactId + "/" + version + "/")
    return file.exists()
}
// END Building Spigot and Bukkit
